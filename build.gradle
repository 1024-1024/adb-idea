buildscript {

    ext.kotlin_version = '1.0.4'

    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

plugins {
    id "org.jetbrains.intellij" version "0.1.10"
}

sourceCompatibility = 1.8
targetCompatibility = 1.8



apply plugin: 'java'
apply plugin: 'kotlin'


repositories {
    mavenCentral()
    jcenter()
}

sourceSets {
    main {
        resources {
            exclude 'website/adb_operations_popup.png'
            exclude 'website/find_actions.png'
            exclude 'website/debug_howto.png'
        }
    }
}

if (!hasProperty('StudioCompilePath')) {
    throw new GradleException("No StudioCompilePath value was set, please create gradle.properties file")
}

intellij {
    version 'IC-15.0.5' // this value is not important, as long as you can compile

    pluginName 'adb_idea'
    updateSinceUntilBuild false

    intellij.alternativeIdePath = project.hasProperty("StudioRunPath") ? StudioRunPath : StudioCompilePath
}

group 'com.developerphil.intellij.plugin.adbidea'

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib:${kotlin_version}"
    compile "org.jetbrains.kotlin:kotlin-runtime:${kotlin_version}"
    compileOnly fileTree(dir: "$StudioCompilePath/plugins/android/lib", include: ['*.jar'])
    compileOnly fileTree(dir: "$StudioCompilePath/lib", include: ['*.jar'])

    testCompile 'junit:junit:4.12'
    testCompile fileTree(dir: "$StudioCompilePath/plugins/android/lib", include: ['*.jar'])
    testCompile fileTree(dir: "$StudioCompilePath/lib", include: ['*.jar'])


}

configurations {
    compile.exclude module: 'idea'
}

task(verifySetup) << {

    def ideaJar = "$StudioCompilePath/lib/idea.jar"
    if (!file(ideaJar).exists()) {
        throw new GradleException("$ideaJar not found, set StudioCompilePath in gradle.properties")
    }
}


compileJava.dependsOn verifySetup
